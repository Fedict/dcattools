# Map publisher from Flanders URI to Crossroad Bank Enterprises URI

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dgov: <http://data.gov.be#>
PREFIX geodcat: <http://data.europa.eu/930/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>

DELETE 
	{	?dataset ?prop ?org .
		?org ?p ?o }
INSERT 
	{ 	?dataset ?prop ?uri .
		?uri a foaf:Organization .
		?uri dgov:org ?drupal .
		?uri ?p ?o .
		?uri foaf:name ?name }
WHERE
	{	?dataset ?prop ?org .
		?org ?p ?o .
		?uri skos:closeMatch ?org .
		?uri skos:exactMatch ?drupal .
		?uri skos:prefLabel ?name 
		VALUES ?prop { dcterms:publisher dcterms:contributor dcterms:creator dcterms:rightsHolder 
					geodcat:custodian geodcat:distributor }
		FILTER (! CONTAINS(STR(?org), "belgif"))
	}