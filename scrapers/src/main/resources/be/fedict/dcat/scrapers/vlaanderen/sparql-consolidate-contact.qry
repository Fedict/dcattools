# Consolidate contact info and distributor
# Use 1 URI instead of different blank nodes / genid

PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX eu: <http://data.europa.eu/930/>

DELETE 
	{	?dataset ?rel ?gencontact .
		?gencontact vcard:fn ?name .
		?gencontact ?p ?o }
INSERT
	{	?dataset ?rel ?contact .
		?contact a vcard:Organization .
		?contact vcard:fn ?name .
		?contact ?p ?o }
WHERE
	{ 	?dataset ?rel ?gencontact .
		?gencontact ?p ?o .
		?gencontact vcard:fn ?name .
		VALUES ?rel { dcat:contactPoint eu:distributor }
		BIND(IRI(
				CONCAT("http://data.gov.be/.well-known/genid/contact/", SHA1(LCASE(?name)))
			)
		AS ?contact)
		FILTER(?p != vcard:fn)
	}
