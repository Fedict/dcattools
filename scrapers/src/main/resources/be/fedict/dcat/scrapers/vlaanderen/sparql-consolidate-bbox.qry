# Consolidate bounding boxes and move them from locn:geometry to dcat:bbox
# Use 1 URI instead of different blank nodes / genid

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX locn: <http://www.w3.org/ns/locn#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX fn: <http://data.gov.be/sparql/function/>

DELETE 
	{	?s dcterms:spatial ?genspatial .
		?genspatial ?p ?o }
INSERT
	{	?s dcterms:spatial ?spatial .
		?spatial dcat:bbox ?simple }
WHERE
	{ 	?s dcterms:spatial ?genspatial .
		?genspatial ?p ?o .
		?genspatial locn:geometry ?bbox
		BIND (
			fn:simplifyPolygon(STR(?bbox))
			as ?simple)
		BIND (IRI(
				CONCAT("http://data.gov.be/.well-known/genid/spatial/", SHA1(?simple))
			)
		AS ?spatial)
		FILTER CONTAINS(STR(?bbox), "POLYGON((")
	}