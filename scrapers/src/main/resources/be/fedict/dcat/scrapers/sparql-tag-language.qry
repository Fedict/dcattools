# Add language tag on literals based on dcterms:language (if the tag was missing)

PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dcterms: <http://purl.org/dc/terms/>

DELETE 
	{ 	?s ?p ?o }
INSERT
	{ 	?s ?p ?literal }
WHERE
	{ 	?s ?p ?o .
		?s dcterms:language ?lang .
		VALUES (?p) {
			( dcterms:title )
			( dcterms:description )
			( dcterms:spatial )
			( dcat:keyword )
			( dcat:theme )
		}
		FILTER ( isLiteral(?o) && langMatches(lang(?o), ""))
		BIND ( STRLANG(?o, ?code) as ?literal)
		VALUES (?lang ?code) {
			( <http://publications.europa.eu/resource/authority/language/NLD> "nl" )
			( <http://publications.europa.eu/resource/authority/language/FRA> "fr" )
			( <http://publications.europa.eu/resource/authority/language/ENG> "en" )
			( <http://publications.europa.eu/resource/authority/language/DEU> "de" )
		}
	}
