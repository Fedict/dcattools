# Add distribution with all properties (note that download is actually XLSX despite the CSV label)

PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dgov: <http://data.gov.be#>
PREFIX dcterms: <http://purl.org/dc/terms/>

INSERT
	{ 	?dataset dcat:distribution ?dist .
		?dist a dcat:Distribution .
		?dist dcterms:title ?title .
		?dist dcterms:language ?lang .
		?dist dcat:accessURL ?access .
		?dist dcat:downloadURL ?download .
		?dist dgov:mediaType <http://data.gov.be/en/taxonomy/term/62> .
		?dist dcterms:format <http://publications.europa.eu/resource/authority/file-type/XLSX>  .
		?dist dcat:mediaType "application/vnd.openxmlformats-officedocument.spreadsheetml.sheet" ; .
		<http://data.gov.be/en/taxonomy/term/62> a dcterms:mediaTypeOrExtent  .
		<http://data.gov.be/en/taxonomy/term/62> rdfs:label "XLSX"  }
WHERE
	{ 	?dataset a dcat:Dataset .
		?dataset dcterms:title ?title .
		?dataset dcat:keyword ?keyw .
		BIND (LANG(?title) as ?langcode) .
		BIND (IRI(CONCAT(
					REPLACE(STR(?dataset), "dataset", "dist"), 
				"/", ?langcode)) as ?dist) .
		BIND (IRI(CONCAT("https://indicators.be/", ?langcode, "/i/", ?keyw, "/", ?langcode)) as ?access).
		BIND (IRI(CONCAT("https://indicators.be/", ?langcode, "/x/", ?keyw, "/download+csv")) as ?download)
		VALUES (?lang ?langcode) {
			( <http://publications.europa.eu/resource/authority/language/NLD> "nl" )
			( <http://publications.europa.eu/resource/authority/language/FRA> "fr" )
			( <http://publications.europa.eu/resource/authority/language/ENG> "en" )
			( <http://publications.europa.eu/resource/authority/language/DEU> "de" ) 
		}
}