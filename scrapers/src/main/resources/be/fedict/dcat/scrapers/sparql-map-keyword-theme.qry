# Map keyword or existing subject / theme to a DCAT-AP theme

PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dgov: <http://data.gov.be#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

DELETE
	{	?dataset dcat:theme ?uri }
INSERT 
	{ 	?dataset dgov:theme ?drupal .
		?dataset dcat:theme ?theme }
WHERE	
	{
		{
			?dataset dcat:keyword ?label .
			?theme skos:altLabel ?label .
			?theme skos:exactMatch ?drupal 
		}
		UNION
		{
			?dataset ?rel ?uri .
			?theme skos:closeMatch ?uri .
			?theme skos:exactMatch ?drupal 
			VALUES	?rel { dcat:theme dcterms:subject }
			FILTER (
				!STRSTARTS(STR(?uri), "http://publications.europa.eu")
			)
		}
	}