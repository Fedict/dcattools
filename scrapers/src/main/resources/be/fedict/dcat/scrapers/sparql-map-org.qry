# Map publisher to semi-official Crossroad Bank Enterprises URI

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dgov: <http://data.gov.be#>
PREFIX geodcat: <http://data.europa.eu/930/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>

DELETE 
	{	?dataset ?prop ?org .
		?org ?p ?o .
		?org ?rel ?label }
INSERT 
	{ 	?dataset ?prop ?neworg .
		?neworg a foaf:Organization .
		?neworg dgov:org ?drupal .
		?neworg ?p ?o .
		?neworg foaf:name ?name }
WHERE
	{	?dataset ?prop ?org .
		?org ?rel ?label .
		?org ?p ?o .
		?neworg skos:altLabel ?label .
		?neworg skos:exactMatch ?drupal .
		?neworg skos:prefLabel ?name 
		VALUES ?prop { dcterms:publisher dcterms:contributor dcterms:creator dcterms:rightsHolder 
					geodcat:custodian geodcat:distributor }
		VALUES ?rel { foaf:name vcard:fn }
		FILTER (?p != ?rel)
		FILTER (! CONTAINS(STR(?org), "belgif"))
	}