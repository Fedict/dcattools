# Set datatype on created/issues/modified/startDate/endDate

PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

DELETE	{	?s ?p ?o }
INSERT 	{	?s ?p ?dtype }
WHERE	{
			?s ?p ?o .
			VALUES ?p {
				dcat:startDate
				dcat:endDate
				dcterms:created
				dcterms:issued
				dcterms:modified
			} .
			BIND(
				STRDT(STR(?o),
					IF(STRLEN(STR(?o)) = 4, xsd:gYear,
						IF(STRLEN(STR(?o)) = 10, xsd:date, 
							xsd:dateTime)
						)
				) AS ?dtype
			)
			FILTER ( datatype(?o) = xsd:string )
}