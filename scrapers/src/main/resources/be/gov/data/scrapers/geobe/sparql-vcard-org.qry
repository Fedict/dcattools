# Map creator to publisher based on organization name of contact point

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>

DELETE 
	{	?dataset dcterms:publisher ?publ }
INSERT 
	{ 	?dataset dcterms:publisher ?neworg .
		?neworg a foaf:Organization .
		?neworg foaf:name ?name }
WHERE
	{	?dataset a ?cl .
		?dataset dcat:contactPoint ?contact .
		?contact vcard:organization-name ?label .
		?neworg skos:altLabel ?label .
		?neworg skos:prefLabel ?name 
		OPTIONAL { ?dataset dcterms:publisher ?publ }
		VALUES ?cl { dcat:Dataset dcat:DataService	}
	}