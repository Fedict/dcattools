# Use applicable legislation property instead of conforms to standard

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dcatap:  <http://data.europa.eu/r5r/>

DELETE 
	{	?s dcterms:conformsTo ?uri .
		?uri dcterms:title ?lbl .
		?uri a dcterms:Standard }
INSERT 
	{ 	?s dcatap:applicableLegislation ?eli }
WHERE 
	{	?s dcterms:conformsTo ?uri .
		?uri dcterms:title ?lbl
		OPTIONAL { ?uri a dcterms:Standard }
		VALUES (?str ?eli)  {
			(" 976/2009 "  <http://data.europa.eu/eli/reg/2009/976/oj> )
			(" 2023/138 " <http://data.europa.eu/eli/reg_impl/2023/138/oj> )
			(" 1089/2010 " <http://data.europa.eu/eli/reg/2010/1089/oj> )
		}
		FILTER (CONTAINS(STR(?lbl), ?str))
	}