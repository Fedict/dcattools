# Explicit classes improving SHACL validation scores

PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dcatap: <http://data.europa.eu/r5r/>
PREFIX dcterms:	<http://purl.org/dc/terms/>
PREFIX eli:  <http://data.europa.eu/eli/ontology#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX mobilitydcatap: <https://w3id.org/mobilitydcat-ap#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>

INSERT 
	{ ?o a ?class }
WHERE 
	{
		?s ?rel ?o .
		FILTER NOT EXISTS { ?o a ?class }
		VALUES (?rel ?class) { 
			( dcat:accessURL rdfs:Resource )
			( dcat:downloadURL rdfs:Resource )
			( dcterms:license dcterms:LicenseDocument )
			( dcterms:publisher foaf:Agent )
			( dcterms:creator foaf:Agent )
			( dcterms:contributor foaf:Agent )
			( dcterms:accrualPeriodicity dcterms:Frequency )
			( dcterms:license dcterms:LicenseDocument )
			( dcterms:rights dcterms:RightsStatement )
			( dcterms:spatial dcterms:Location )
			( dcat:accessService dcat:DataService )
			( dcat:inSeries dcat:DatasetSeries )
			( dcterms:format dcterms:MediaTypeOrExtent )
			( dcat:landingPage foaf:Document )
			( dcat:mediaType dcterms:MediaType )
			( foaf:page foaf:Document )
			( foaf:homepage foaf:Document )
			( dcat:servesDataset dcat:Dataset )
			( dcterms:conformsTo dcterms:Standard )
			( dcterms:temporal dcterms:PeriodOfTime )
			( dcat:contactPoint vcard:Kind )
			( dcterms:type skos:Concept )
			( dcat:theme skos:Concept )
			( dcat:themeTaxonomy skos:ConceptScheme )
			( adms:identifier adms:Identifier )
			( org:memberOf foaf:Agent )
			( mobilitydcatap:mobilityDataStandard mobilitydcatap:MobilityDataStandard )
			( dcatap:applicableLegislation eli:LegalResource )
		}
	}


