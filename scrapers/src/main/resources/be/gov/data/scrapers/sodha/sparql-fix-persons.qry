# Add foaf:Agent to creators and contributors

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

DELETE
	{	?s ?rel ?name }
INSERT 
	{	?s ?rel ?hash .
		?hash a foaf:Agent .
		?hash a foaf:Person .
		?hash foaf:name ?name .
		?hash foaf:givenName ?given .
		?hash foaf:familyName ?family
	}
WHERE
	{	?s ?rel ?name .
		VALUES ?rel { dcterms:creator dcterms:contributor }
		BIND (
			IRI(
				CONCAT("http://data.gov.be/.well-known/genid/", SHA1(?name))
			)
		as ?hash)
		BIND (IF(
					CONTAINS(?name, ","), STRBEFORE(?name, ","), ?unbound) 
				as ?family)
		BIND (IF(
					CONTAINS(?name, ","), STRAFTER(?name, ", "), ?unbound)
				 as ?given)

	}