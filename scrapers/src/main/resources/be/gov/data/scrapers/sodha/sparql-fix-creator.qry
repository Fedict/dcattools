# Add foaf:Agent to creators

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

DELETE
	{	?s dcterms:creator ?name }
INSERT 
	{	?s dcterms:creator ?hash .
		?hash a foaf:Agent .
		?hash a foaf:Person .
		?hash foaf:name ?name .
		?hash foaf:givenName ?given .
		?hash foaf:familyName ?family
	}
WHERE
	{	?s dcterms:creator ?name .
		BIND (
			IRI(
				CONCAT("http://data.gov.be/.well-known/genid/", SHA1(?name))
			)
		as ?hash)
		BIND (IF(
					CONTAINS(?name, ","), STRBEFORE(?name, ","), ?unbound) 
				as ?family)
		BIND (IF(
					CONTAINS(?name, ","), STRAFTER(?name, ", "), ?unbound)
				 as ?given)

	}