# Create an URI based on start and end date
# Using this instead of blank nodes or other URIs to reuse (and reduce the number of) triples

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX schema:  <http://schema.org/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>

DELETE 
	{	?s dcterms:temporal ?uri .
		?uri a dcterms:PeriodOfTime .
		?uri ?startdate ?start .
		?uri ?enddate ?end }
INSERT 
	{ 	?s dcterms:temporal ?newuri .
		?newuri a dcterms:PeriodOfTime .
		?newuri dcat:startDate ?start .
		?newuri dcat:endDate ?end }
WHERE 
	{	?s dcterms:temporal ?uri .
		?uri ?startdate ?start .
		OPTIONAL { ?uri ?enddate ?end }
		OPTIONAL { ?uri a dcterms:PeriodOfTime }
		VALUES ?startdate { dcat:startDate schema:startDate }
		VALUES ?enddate {  dcat:endDate schema:endDate }
		BIND (
			IRI(
				CONCAT("https://data.gov.be/.well-known/genid/temporal/", 
					REPLACE(CONCAT(STR(?start), '_', STR(?end)), '[ :]', '-')
					)
				)
		as ?newuri)
	}