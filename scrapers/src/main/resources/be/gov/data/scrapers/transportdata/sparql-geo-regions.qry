# Set geographic area of a dataset or dataservice to a Region

PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>

DELETE 
	{ 	?res dcterms:spatial ?geo .
		?geo dcterms:identifier ?id .
		?res dcterms:spatial ?geo4 .
		?geo4 dcat:bbox ?o 
	}
INSERT 
	{
		?res dcterms:spatial ?uri  .
		?uri a dcterms:Location .
		?uri dcat:bbox ?bbox
	}
WHERE
	{	?res a ?cl
		VALUES ?cl { dcat:Dataset dcat:DataService }
		?res dcterms:spatial ?geo .
		?geo dcterms:identifier ?id
		VALUES (?id ?uri ?bbox) {
			( <http://data.europa.eu/nuts/code/BE1> <https://sws.geonames.org/2800867/> "POLYGON((4.24 50.92,4.48 50.92,4.48 50.76,4.24 50.76,4.24 50.92))"^^geo:wktLiteral )
			( <http://data.europa.eu/nuts/code/BE2> <https://sws.geonames.org/3337388/> "POLYGON((2.54 51.51,5.92 51.51,5.92 50.67,2.54 50.67,2.54 51.51))"^^geo:wktLiteral )
			( <http://data.europa.eu/nuts/code/BE3> <https://sws.geonames.org/3337387/> "POLYGON((2.54 50.85,6.41 50.85,6.41 49.49,2.54 49.49,2.54 50.85))"^^geo:wktLiteral )
		}
		OPTIONAL {
			?res dcterms:spatial ?geo4 .
			?geo4 dcat:bbox ?o 
		}
	}
