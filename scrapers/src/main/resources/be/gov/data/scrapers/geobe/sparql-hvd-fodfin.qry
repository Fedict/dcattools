# Add HvD info to specific FOD FIN datasets / services

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dcatap: <http://data.europa.eu/r5r/>

INSERT
	{	?dataset dcatap:applicableLegislation <http://data.europa.eu/eli/reg_impl/2023/138/oj> .
		?dataset dcatap:hvdCategory ?cat .
		?dist dcatap:applicableLegislation <http://data.europa.eu/eli/reg_impl/2023/138/oj> .
		}
WHERE
	{ 
		?dataset a ?cl .
		?dataset dcterms:title ?title .
		OPTIONAL { ?dataset dcat:distribution ?dist }
		VALUES ?cl { dcat:Dataset dcat:DataService }
		VALUES (?label ?cat) {
			("Administrative units" <http://data.europa.eu/bna/c_9427236f> )
			("Cadastral parcels" <http://data.europa.eu/bna/c_6a3f6896> )
			("Land use" <http://data.europa.eu/bna/c_ad9ae929> )
		}
		FILTER (LANG(?title) = 'en' && CONTAINS(?title, ?label))
	}
